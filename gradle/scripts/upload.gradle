apply plugin: 'maven-publish'
apply plugin: 'curseforge'

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'CCTweaks'
		licenses = ['MIT']
		vcsUrl = 'https://github.com/SquidDev-CC/CCTweaks'

		version {
			name = project.version
			desc = 'Random additions to ComputerCraft'
			released = new Date()
			vcsTag = 'v' + project.version
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
			artifact devJar

			artifact apiJar
			artifact sourcesJar
			artifact javadocJar
		}
	}
}

curse {
	projectId = '232180'
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''
	releaseType = 'release'

	additionalArtifact devJar

	relatedProject 'computercraft' : 'requiredLibrary'
}

task curseforge(dependsOn: curse)
